// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.isTravis = 'true' == System.getenv('TRAVIS')
    ext.preDexEnabled = 'true' == System.getProperty('pre-dex', 'true')

    // set Geocaching API staging key and secret if production key and secret is not set
    // Note: Staging server is slow and not for production use!!!!
    if (!project.hasProperty('geocachingApiKey')) {
        ext.geocachingApiKey = '9C7552E1-3C04-4D04-A395-230D8931E494'
    }
    if (!project.hasProperty('geocachingApiSecret')) {
        ext.geocachingApiSecret = 'DA7CC147-7B5B-4423-BCB4-D0C03E2BF685'
    }
    if (!project.hasProperty('geocachingApiStaging')) {
        ext.geocachingApiStaging = true
    }

    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'io.fabric.tools:gradle:1.24.1'
    }
}

allprojects {
    apply plugin: 'com.github.ben-manes.versions'

    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://maven.arcao.com' }
    }

    // dependencyUpdates: suggest only released artifacts
    //dependencyUpdates.resolutionStrategy = {
    //    componentSelection { rules ->
    //        rules.all { ComponentSelection selection ->
    //            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { selection.candidate.version ==~ /(?i).*[.-]${it}.*/ }
    //            if (rejected)
    //                selection.reject('Release candidate')
    //        }
    //    }
    //}
}

def gitSha() {
    return 'git rev-parse --short HEAD'.execute([], rootDir).text.trim()
}

def gitTimestamp() {
    return  'git log -n 1 --format=%aI'.execute([], rootDir).text.trim()
}

def gitVersionCode() {
    return 'git rev-list --count HEAD'.execute([], rootDir).text.trim().toInteger()
}