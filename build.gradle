buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-alpha3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'io.fabric.tools:gradle:1.22.2'
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    jcenter()
    maven { url 'https://maven.google.com' }
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'http://maven.arcao.com' }
}

dependencies {
    //def sdkVer = '25.4.0'
    def sdkVer = '26.0.0-beta2'

    //noinspection GradleCompatible
    implementation "com.android.support:support-v4:$sdkVer"
    implementation "com.android.support:appcompat-v7:$sdkVer"
    implementation "com.android.support:recyclerview-v7:$sdkVer"
    implementation "com.android.support:cardview-v7:$sdkVer"
    implementation "com.android.support:design:$sdkVer"

    // from maven.arcao.com repository
    implementation('com.arcao:geocaching-api:2.0.1') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        // Not working in Android, slf4j-timber used instead
    }
    implementation 'com.asamm:locus-api-android:0.2.9'

    // from JCenter repository
    implementation 'com.arcao:slf4j-timber:3.0'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.github.scribejava:scribejava-core:4.1.1'
    implementation 'com.github.scribejava:scribejava-httpclient-okhttp:4.1.1'
    implementation 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    implementation 'com.afollestad.material-dialogs:core:0.9.4.5'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.6.8'
    implementation 'com.squareup.okhttp3:okhttp:3.8.0'

    annotationProcessor 'com.google.auto.value:auto-value:1.4.1'
    compileOnly 'com.google.auto.value:auto-value:1.4.1'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
    implementation 'com.ryanharter.auto.value:auto-value-parcel-adapter:0.2.5'
}

def gitSha() {
    return 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ", TimeZone.getTimeZone('UTC'))
}

def gitVersionCode() {
    return 'git rev-list --count HEAD'.execute([], project.rootDir).text.trim().toInteger()
}

def isTravis = 'true' == System.getenv('TRAVIS')
def preDexEnabled = 'true' == System.getProperty('pre-dex', 'true')

// set Geocaching API staging key and secret if production key and secret is not set
// Note: Staging server is slow and not for production use!!!!
if (!project.hasProperty('geocachingApiKey')) {
    ext.geocachingApiKey = '9C7552E1-3C04-4D04-A395-230D8931E494'
}
if (!project.hasProperty('geocachingApiSecret')) {
    ext.geocachingApiSecret = 'DA7CC147-7B5B-4423-BCB4-D0C03E2BF685'
}
if (!project.hasProperty('geocachingApiStaging')) {
    ext.geocachingApiStaging = true
}

android {
    compileSdkVersion 26
    buildToolsVersion '25.0.3'

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries preDexEnabled && !isTravis
    }

    defaultConfig {
        minSdkVersion hasProperty('devBuild') ? 25 : 15
        targetSdkVersion 25
        versionCode gitVersionCode()
        buildConfigField 'String', 'GIT_SHA', 'null'
        buildConfigField 'String', 'BUILD_TIME', 'null'
        buildConfigField 'String', 'GEOCACHING_API_KEY', "\"${geocachingApiKey}\""
        buildConfigField 'String', 'GEOCACHING_API_SECRET', "\"${geocachingApiSecret}\""
        buildConfigField 'boolean', 'GEOCACHING_API_STAGING', "${geocachingApiStaging}"

        resConfigs 'en', 'cs', 'da', 'de', 'es', 'fr', 'hr', 'it', 'nl', 'pl', 'pt', 'ru', 'sk', 'uk', 'zh', 'zh-rTW'
    }

    compileOptions {
        encoding 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias = 'geocaching4locus'
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
            versionNameSuffix '-dev'
        }
        release {
            versionNameSuffix ''

            buildConfigField 'String', 'GIT_SHA', '"' + gitSha() +'"'
            buildConfigField 'String', 'BUILD_TIME', '"' + buildTime() + '"'

            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard.pro'
        }
    }

    packagingOptions {
        // removed because of conflict in creating package
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        // unused files, removed to minimize APK file
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'org/apache/http/version.properties'
        exclude 'templates/release-notes.vm'
        exclude 'log4j.xml'
    }
    lintOptions { abortOnError false }
}

// dependencyUpdates: suggest only released artifacts
//dependencyUpdates.resolutionStrategy = {
//    componentSelection { rules ->
//        rules.all { ComponentSelection selection ->
//            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { selection.candidate.version ==~ /(?i).*[.-]${it}.*/ }
//            if (rejected)
//                selection.reject('Release candidate')
//        }
//    }
//}

if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = android.signingConfigs.debug
}