apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compile_sdk_version

    defaultConfig {
        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
        minSdkVersion rootProject.min_sdk_version
        targetSdkVersion rootProject.target_sdk_version

        multiDexEnabled true

        buildConfigField 'String', 'GEOCACHING_API_KEY', "\"${geocachingApiKey}\""
        buildConfigField 'String', 'GEOCACHING_API_SECRET', "\"${geocachingApiSecret}\""
        buildConfigField 'boolean', 'GEOCACHING_API_STAGING', "${geocachingApiStaging}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        freeCompilerArgs += [
                "-opt-in=kotlin.RequiresOptIn",
                "-Xjvm-default=all-compatibility"
        ]
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        junitPlatform {
            // Configuration goes here!
        }
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/LICENSE*']
        }
        resources {
            excludes += ['META-INF/LICENSE*']
        }
    }

    namespace 'com.arcao.geocaching4locus.geocaching_api'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"

    api "com.github.scribejava:scribejava-core:$scribejava_version"
    api "com.github.scribejava:scribejava-httpclient-okhttp:$scribejava_version"
    implementation "com.jakewharton.timber:timber:$timber_version"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_jdk_libs_version"

    // koin
    implementation "io.insert-koin:koin-android:$koin_version"

    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    androidTestImplementation "androidx.test:runner:$test_runner_version"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:$android_test_junit"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:$android_test_junit"
}
