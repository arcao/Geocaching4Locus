repositories {
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint Deps.ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

task ktlintIde(type: JavaExec, group: "formatting") {
    description = "Apply code style to IntelliJ Idea/Android Studio IDE"
    classpath = configurations.ktlint
    workingDir =  project.rootDir
    main = "com.github.shyiko.ktlint.Main"
    args "--apply-to-idea-project", "--android", "-y"
}